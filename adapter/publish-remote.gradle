// Add the plugins required to publish and sign the artifact.
apply plugin: 'maven-publish'
apply plugin: 'signing'

// Declare the sources to ensure the correct source files are packed with executable, compiled code.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourcesJar
}

// Set two properties: groupdID and version of the artifact.
group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

// Provide the metadata of the library to release
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                System.out.println("\n------------ publish-remote.gradle  -----------")
                // The coordinates of the library, being set from variables that we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                System.out.println("PUBLISH_GROUP_ID\t\t: " + PUBLISH_GROUP_ID)
                System.out.println("PUBLISH_ARTIFACT_ID\t\t: " + PUBLISH_ARTIFACT_ID)
                System.out.println("PUBLISH_VERSION\t\t\t: " + PUBLISH_VERSION)


                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifacts javaDocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Tempo Mediation Adapter for AppLovin"
                    url = 'https://github.com/Tempo-Platform/tempo-android-applovin-mediation'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'omnibaker'
                            name = 'omnibaker'
                            email = 'omnibaker@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'git@github.com:Tempo-Platform/tempo-android-applovin-mediation.git'
                        developerConnection = 'git@github.com:Tempo-Platform/tempo-android-applovin-mediation.git'
                        url = 'https://github.com/Tempo-Platform/tempo-android-applovin-mediation'
                    }
                }
            }
        }
    }
}

// Get the signing details from the root project and sign the artifact.
signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}